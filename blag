#! /bin/awk -f

#  blag	    Noah Thompson	|	2019
#
# blag is a utility for converting ASCII to HTML
# the name of the file becomes the title
# lines beginning with a tab become paragraphs
# regular lines are headers
# use css=style.css

BEGIN { if( CSS == "" ) CSS = "style.css";
        unformatted = "false"; #
        print "<!DOCTYPE HTML> <head> <title>" ARGV[1] "</title> <link rel=\"stylesheet\" href = \"" CSS "\"></head> <body><div class = \"content\">" }

#[image fp ] means insert "fp" as image
/^\[image/{print "<img src=\"" $2 "\">"; next}

#[code] tag delineates text to be left untouched
/^\[code\]/  {unformatted = "true"; print "<xmp>"; next}
/^\[\/code\]/{unformatted = "false"; print "</xmp>"; next}
/.*/         {if(unformatted == "true"){print $0; next} }

#____ -> 4 chars before TOC entry string
#N.C)ANCHOR_NAME -> TOC entry
#HEADER_NAME      -> Anchor point
#if a header name matches a TOC entry, they link to each other
/^.\.*)/{print "<a href=\"#" substr($0,5) "\">" $0 "</a><br/>"; next } #N.C) -> table entry N sub C (substring gets N and dot and C)
/\t/    {print "<p>"$0"</p>"; next  }
/..*/   {print "<h1><a name=\""$0"\">" $0"</a></h1>"; next }

END { print "</div></body> </html>" }
